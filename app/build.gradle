apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply from: '../config/quality.gradle'

import com.android.ddmlib.DdmPreferences

import java.util.concurrent.TimeUnit

// Copy the signing.properties.sample file to ~/.sign/signing.properties and adjust the values.
final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = PROD_PROPS_FILE // new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)

final int ADB_TIMEOUT = TimeUnit.MINUTES.toMillis(5)
final boolean continuousIntegrationBuild = System.getenv('JENKINS_HOME') != null
/*
final boolean preDexEnabled = hasProperty('pre.dex') ?
        Boolean.valueOf(getProperty('pre.dex').toString()) :
        !continuousIntegrationBuild
if (!preDexEnabled) {
    println 'Pre-dexing disabled.'
}
*/
/*
afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = ['--multi-dex']
        } else {
            dx.additionalParameters += '--multi-dex'
        }
    }
}
*/
afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += '--multi-dex' // enable multidex
//        dx.additionalParameters += "--main-dex-list=$projectDir/multidex_keep.txt".toString()
    }
}
if (continuousIntegrationBuild) {
    DdmPreferences.setTimeOut(ADB_TIMEOUT)
    println "Device timeout is ${DdmPreferences.getTimeOut()}ms"
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

def computeVersionName(label) {
    return "2.1.${android.defaultConfig.versionCode}-${label}-${date}"
}

final JavaVersion JAVA_VERSION = JavaVersion.VERSION_1_7
if (JavaVersion.current() != JAVA_VERSION) {
    throw new IllegalStateException("Incorrect Java Development Kit version; expected "
            + JAVA_VERSION + " but found " + JavaVersion.current())
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    compileOptions {
        sourceCompatibility = JAVA_VERSION
        targetCompatibility = JAVA_VERSION
        encoding = 'utf-8'
    }
    dexOptions {
        preDexLibraries = true
        javaMaxHeapSize "4096M"
    }
    defaultConfig {
        applicationId 'org.xowa'
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 154
        testApplicationId 'org.wikipedia.test'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }

    sourceSets {
        test {
            java.srcDirs += 'src/testlib/java'
        }
        androidTest {
            java.srcDirs += 'src/testlib/java'
        }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            // COMMENTED: causes APK to fail; 2016-06-03
            // minifyEnabled true
            // shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            versionName computeVersionName("dev")
            applicationId 'org.xowa.dev'
            signingConfig signingConfigs.debug
        }
        prod {
            versionName computeVersionName("r")
            applicationId 'org.xowa'
            signingConfig signingConfigs.prod
        }
        releasesprod {
            versionName computeVersionName("releasesprod")
            signingConfig signingConfigs.prod
        }
        alpha {
            versionName computeVersionName("alpha")
            applicationId 'org.xowa'
            signingConfig signingConfigs.debug
        }
        beta {
            versionName computeVersionName("beta")
            applicationIdSuffix 'beta'
            signingConfig signingConfigs.prod
        }
        amazon {
            versionName computeVersionName("amazon")
            signingConfig signingConfigs.prod
        }
        custom {
            versionName computeVersionName(customChannel)
            applicationId getProperty('customApplicationId')
            // next line is for injecting a custom channel value into the custom/AndroidManifest.xml
            manifestPlaceholders = [customChannel: getProperty('customChannel').toString()]
            signingConfig signingConfigs.prod
        }
        xowa {  // XOWA
            versionName computeVersionName("xowa")
            applicationId 'org.xowa'
            signingConfig signingConfigs.debug
        }
    }
    // while we still have lint errors; remove once those are fixed
    lintOptions {
        disable 'MissingTranslation'
        warning 'NewApi' // until https://code.google.com/p/android/issues/detail?id=137195 is released
        disable 'InvalidPackage' // required by Butter Knife
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor' // required by Butter Knife

        // For Espresso testing libraries. See http://stackoverflow.com/q/33800924/970346.
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    }
}

def compileSubprojectOrModule = { String projectName,
                                  String moduleName,
                                  String excludeGroup = null,
                                  String excludeModule = null ->
    def dependency = project.findProject(projectName) ?: moduleName
    project.dependencies.add('compile', dependency, {exclude group: excludeGroup, module: excludeModule })
}

dependencies {
    compileSubprojectOrModule ':java-mwapi:json', 'org.mediawiki.api:json:1.3.2', 'org.json', 'json'
    // To keep the Maven Central dependencies up-to-date

    // use http://gradleplease.appspot.com/ or http://search.maven.org/.
    compile 'com.android.support:multidex:1.0.1'
    // Debug with ./gradlew -q app:dependencies --configuration compile
    compile 'com.android.support:appcompat-v7:23.1.1'
    // includes support-v4
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:percent:23.1.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.6.0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.squareup:otto:1.3.6'
    compile 'com.squareup.picasso:picasso:2.5.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.mobsandgeeks:android-saripaar:1.0.3'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.github.ryanjohn1:onboarding:1.0.3'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'com.mapbox.mapboxsdk:mapbox-android-sdk:2.3.0@aar'
    compile 'net.hockeyapp.android:HockeySDK:3.6.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'com.squareup.okhttp:mockwebserver:2.6.0'
    // Required by Android JUnit Runner.
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    // JUnit Rules
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    // Android JUnit Runner
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks,

    // CountingIdlingResource
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    // Espresso-web for WebView support
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.1'
    // Espresso-idling-resource for synchronization with background jobs.
    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:2.2.1'
    compile project(':_100_core')
    compile project(':_110_gfml')
    compile project(':_140_dbs')
    compile project(':_150_gfui')
    compile project(':_400_xowa')
}

private setSigningConfigKey(config, Properties props) {
    if (props) {
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

@Nullable private Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}
